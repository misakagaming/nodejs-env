       +->   CYYY0211_CHILD_CREATE             07/05/2023  15:08
       !       IMPORTS:
       !         Work View imp_reference iyy1_server_data (Transient, Mandatory, Import only)
       !           userid
       !           reference_id
       !         Entity View imp child (Transient, Mandatory, Import only)
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !           csearch_attr_text
       !           cother_attr_text
       !       EXPORTS:
       !         Entity View exp child (Transient, Export only)
       !           cinstance_id
       !           creference_id
       !         Work View exp_error iyy1_component (Transient, Export only)
       !           severity_code
       !           rollback_indicator
       !           origin_servid
       !           context_string
       !           return_code
       !           reason_code
       !           checksum
       !       LOCALS:
       !         Entity View loc_imp child
       !           cinstance_id
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !           csearch_attr_text
       !           cother_attr_text
       !         Entity View loc_empty child
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !           csearch_attr_text
       !           cother_attr_text
       !         Work View loc_error iyy1_component
       !           severity_code
       !           rollback_indicator
       !           origin_servid
       !           context_string
       !           return_code
       !           reason_code
       !           checksum
       !         Group View (9) loc_group_context
       !           Work View loc_g_context dont_change_text
       !             text_150
       !         Work View loc dont_change_return_codes
       !           1_ok
       !           n20_mandatory_import_missing
       !         Work View loc dont_change_reason_codes
       !           1_default
       !           124_child_parent_key_attr_mand
       !           125_child_key_attr_mand
       !           126_child_search_attr_mand
       !
       !     PROCEDURE STATEMENTS
       !
     1 !  NOTE:
     1 !  See the description for the purpose.
     1 !
     2 !  NOTE:
     2 !  RELEASE HISTORY
     2 !  01_00 23-02-1998 New release
     2 !
     3 !  USE cyyy9001_exception_hndlng_dflt
     3 !     WHICH EXPORTS: Work View exp_error iyy1_component FROM Work View exp_error iyy1_component
     4 !
     5 !  NOTE:
     5 !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
     5 !  Name the method below
     5 !
     6 !  SET SUBSCRIPT OF loc_group_context TO 1
     7 !  SET loc_g_context dont_change_text text_150 TO "CYYY0211"
     8 !  SET SUBSCRIPT OF loc_group_context TO 2
     9 !  SET loc_g_context dont_change_text text_150 TO "CREATE"
    10 !
    11 !  NOTE:
    11 !  ****************************************************************
    11 !  The used ReturnCode/ReasonCode values
    11 !
    12 !  NOTE:
    12 !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    12 !  Set the return and reason codes below
    12 !
    13 !  SET loc dont_change_return_codes 1_ok TO 1
    14 !  SET loc dont_change_return_codes n20_mandatory_import_missing TO -20
    15 !
    16 !  SET loc dont_change_reason_codes 1_default TO 1
    17 !  SET loc dont_change_reason_codes 124_child_parent_key_attr_mand TO 124
    18 !  SET loc dont_change_reason_codes 125_child_key_attr_mand TO 125
    19 !  SET loc dont_change_reason_codes 126_child_search_attr_mand TO 126
    20 !
    21 !  NOTE:
    21 !  ****************************************************************
    21 !  Instance id generate
    21 !
    22 !  MOVE imp child TO loc_imp child
    23 !
    24 !  USE cyyy0261_child_instance_id_gen
    24 !     WHICH EXPORTS: Entity View loc_imp child FROM Entity View exp child
    25 !
    26 !  NOTE:
    26 !  ****************************************************************
    26 !  Mandatory or optional only the needed fields for the operation are checked.
    26 !
    27 !  +->IF exp_error iyy1_component return_code >= loc dont_change_return_codes 1_ok
    28 !  !  +->IF loc_imp child cparent_pkey_attr_text <= loc_empty child cparent_pkey_attr_text
    29 !  !  !  SET exp_error iyy1_component return_code TO loc dont_change_return_codes n20_mandatory_import_missing
    30 !  !  !  SET exp_error iyy1_component reason_code TO loc dont_change_reason_codes 124_child_parent_key_attr_mand
    31 ! <------ESCAPE
    28 !  !  +--
    32 !  !  +->IF loc_imp child ckey_attr_num <= loc_empty child ckey_attr_num
    33 !  !  !  SET exp_error iyy1_component return_code TO loc dont_change_return_codes n20_mandatory_import_missing
    34 !  !  !  SET exp_error iyy1_component reason_code TO loc dont_change_reason_codes 125_child_key_attr_mand
    35 ! <------ESCAPE
    32 !  !  +--
    36 !  !  +->IF loc_imp child csearch_attr_text <= loc_empty child csearch_attr_text
    37 !  !  !  SET exp_error iyy1_component return_code TO loc dont_change_return_codes n20_mandatory_import_missing
    38 !  !  !  SET exp_error iyy1_component reason_code TO loc dont_change_reason_codes 126_child_search_attr_mand
    39 ! <------ESCAPE
    36 !  !  +--
    27 !  +--
    40 !
    41 !  NOTE:
    41 !  ****************************************************************
    41 !  The operation is done.
    41 !
    42 !  +->IF exp_error iyy1_component return_code >= loc dont_change_return_codes 1_ok
    43 !  !  USE dyyy0211_child_create
    43 !  !     WHICH IMPORTS: Work View imp_reference iyy1_server_data TO Work View imp_reference iyy1_server_data
    43 !  !                    Entity View loc_imp child TO Entity View imp child
    43 !  !                    Work View exp_error iyy1_component TO Work View imp_error iyy1_component
    43 !  !     WHICH EXPORTS: Entity View exp child FROM Entity View exp child
    43 !  !                    Work View loc_error iyy1_component FROM Work View exp_error iyy1_component
    44 !  !  +->IF loc_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    45 !  !  !  MOVE loc_error iyy1_component TO exp_error iyy1_component
    46 !  !  !  NOTE:
    46 !  !  !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    46 !  !  !  !! WARNING: In D AB, if there' s a need for generating context string, must exit from ESCAPE AB.
    46 !  !  !
    47 ! <------ESCAPE
    44 !  !  +--
    48 !  !  SET exp child cinstance_id TO loc_imp child cinstance_id
    42 !  +--
    49 !
    50 !  +->IF exp_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    51 !  !  USE cyyy9141_context_string_set
    51 !  !     WHICH IMPORTS: Group View  loc_group_context TO Group View imp_group_context
    51 !  !     WHICH EXPORTS: Work View loc_error iyy1_component FROM Work View exp_error iyy1_component
    51 !  !                    Work View exp_error iyy1_component FROM Work View exp_context iyy1_component
    52 !  !
    53 !  !  +->IF loc_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    54 !  !  !  MOVE loc_error iyy1_component TO exp_error iyy1_component
    53 !  !  +--
    55 !  !  SET exp_error iyy1_component severity_code TO "E"
    50 !  +--
       +---

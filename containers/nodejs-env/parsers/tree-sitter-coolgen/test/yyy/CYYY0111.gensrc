       +->   CYYY0111_PARENT_CREATE            07/05/2023  15:08
       !       IMPORTS:
       !         Work View imp_reference iyy1_server_data (Transient, Mandatory, Import only)
       !           userid
       !           reference_id
       !         Entity View imp parent (Transient, Mandatory, Import only)
       !           pkey_attr_text
       !           psearch_attr_text
       !           pother_attr_text
       !           ptype_tkey_attr_text
       !       EXPORTS:
       !         Entity View exp parent (Transient, Export only)
       !           pinstance_id
       !           preference_id
       !         Work View exp_error iyy1_component (Transient, Export only)
       !           severity_code
       !           rollback_indicator
       !           origin_servid
       !           context_string
       !           return_code
       !           reason_code
       !           checksum
       !       LOCALS:
       !         Entity View loc_imp parent
       !           pinstance_id
       !           pkey_attr_text
       !           psearch_attr_text
       !           pother_attr_text
       !           ptype_tkey_attr_text
       !         Entity View loc_empty parent
       !           pinstance_id
       !           pkey_attr_text
       !           psearch_attr_text
       !           pother_attr_text
       !           ptype_tkey_attr_text
       !         Work View loc_error iyy1_component
       !           severity_code
       !           rollback_indicator
       !           origin_servid
       !           context_string
       !           return_code
       !           reason_code
       !           checksum
       !         Group View (9) loc_group_context
       !           Work View loc_g_context dont_change_text
       !             text_150
       !         Work View loc dont_change_return_codes
       !           1_ok
       !           n20_mandatory_import_missing
       !         Work View loc dont_change_reason_codes
       !           1_default
       !           104_parent_type_key_attr_mand
       !           105_parent_key_attr_mand
       !           106_parent_search_attr_mand
       !
       !     PROCEDURE STATEMENTS
       !
     1 !  NOTE:
     1 !  See the description for the purpose.
     1 !
     2 !  NOTE:
     2 !  RELEASE HISTORY
     2 !  01_00 23-02-1998 New release
     2 !
     3 !  USE cyyy9001_exception_hndlng_dflt
     3 !     WHICH EXPORTS: Work View exp_error iyy1_component FROM Work View exp_error iyy1_component
     4 !
     5 !  NOTE:
     5 !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
     5 !  Name the method below
     5 !
     6 !  SET SUBSCRIPT OF loc_group_context TO 1
     7 !  SET loc_g_context dont_change_text text_150 TO "CYYY0111"
     8 !  SET SUBSCRIPT OF loc_group_context TO 2
     9 !  SET loc_g_context dont_change_text text_150 TO "CREATE"
    10 !
    11 !  NOTE:
    11 !  ****************************************************************
    11 !  The used ReturnCode/ReasonCode values
    11 !
    12 !  NOTE:
    12 !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    12 !  Set the return and reason codes below
    12 !
    13 !  SET loc dont_change_return_codes 1_ok TO 1
    14 !  SET loc dont_change_return_codes n20_mandatory_import_missing TO -20
    15 !
    16 !  SET loc dont_change_reason_codes 1_default TO 1
    17 !  SET loc dont_change_reason_codes 104_parent_type_key_attr_mand TO 104
    18 !  SET loc dont_change_reason_codes 105_parent_key_attr_mand TO 105
    19 !  SET loc dont_change_reason_codes 106_parent_search_attr_mand TO 106
    20 !
    21 !  NOTE:
    21 !  ****************************************************************
    21 !  Instance id generate
    21 !
    22 !  MOVE imp parent TO loc_imp parent
    23 !
    24 !  USE cyyy0161_parent_instance_id_gen
    24 !     WHICH EXPORTS: Entity View loc_imp parent FROM Entity View exp parent
    25 !
    26 !  NOTE:
    26 !  ****************************************************************
    26 !  Mandatory or optional only the needed fields for the operation are checked.
    26 !
    27 !  +->IF exp_error iyy1_component return_code >= loc dont_change_return_codes 1_ok
    28 !  !  +->IF loc_imp parent pkey_attr_text <= loc_empty parent pkey_attr_text
    29 !  !  !  SET exp_error iyy1_component return_code TO loc dont_change_return_codes n20_mandatory_import_missing
    30 !  !  !  SET exp_error iyy1_component reason_code TO loc dont_change_reason_codes 104_parent_type_key_attr_mand
    28 !  !  +--
    31 !  !  +->IF loc_imp parent psearch_attr_text <= loc_empty parent psearch_attr_text
    32 !  !  !  SET exp_error iyy1_component return_code TO loc dont_change_return_codes n20_mandatory_import_missing
    33 !  !  !  SET exp_error iyy1_component reason_code TO loc dont_change_reason_codes 105_parent_key_attr_mand
    31 !  !  +--
    34 !  !  +->IF loc_imp parent ptype_tkey_attr_text <= loc_empty parent ptype_tkey_attr_text
    35 !  !  !  SET exp_error iyy1_component return_code TO loc dont_change_return_codes n20_mandatory_import_missing
    36 !  !  !  SET exp_error iyy1_component reason_code TO loc dont_change_reason_codes 106_parent_search_attr_mand
    34 !  !  +--
    27 !  +--
    37 !
    38 !  +->IF exp_error iyy1_component return_code >= loc dont_change_return_codes 1_ok
    39 !  !  USE dyyy0111_parent_create
    39 !  !     WHICH IMPORTS: Work View exp_error iyy1_component TO Work View imp_error iyy1_component
    39 !  !                    Work View imp_reference iyy1_server_data TO Work View imp_reference iyy1_server_data
    39 !  !                    Entity View loc_imp parent TO Entity View imp parent
    39 !  !     WHICH EXPORTS: Entity View exp parent FROM Entity View exp parent
    39 !  !                    Work View loc_error iyy1_component FROM Work View exp_error iyy1_component
    40 !  !  +->IF loc_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    41 !  !  !  MOVE loc_error iyy1_component TO exp_error iyy1_component
    42 !  !  !  NOTE:
    42 !  !  !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    42 !  !  !  !! WARNING: In D AB, if there' s a need for generating context string, must exit from ESCAPE AB.
    42 !  !  !
    43 ! <------ESCAPE
    40 !  !  +--
    44 !  !  SET exp parent pinstance_id TO loc_imp parent pinstance_id
    38 !  +--
    45 !
    46 !  +->IF exp_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    47 !  !  USE cyyy9141_context_string_set
    47 !  !     WHICH IMPORTS: Group View  loc_group_context TO Group View imp_group_context
    47 !  !     WHICH EXPORTS: Work View loc_error iyy1_component FROM Work View exp_error iyy1_component
    47 !  !                    Work View exp_error iyy1_component FROM Work View exp_context iyy1_component
    48 !  !
    49 !  !  +->IF loc_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    50 !  !  !  MOVE loc_error iyy1_component TO exp_error iyy1_component
    49 !  !  +--
    51 !  !  SET exp_error iyy1_component severity_code TO "E"
    46 !  +--
       +---

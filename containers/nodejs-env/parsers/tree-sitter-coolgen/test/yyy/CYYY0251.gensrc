       +->   CYYY0251_CHILD_LIST               07/05/2023  15:09
       !       IMPORTS:
       !         Work View imp_filter iyy1_list (Transient, Mandatory, Import only)
       !           sort_option
       !           scroll_type
       !           list_direction
       !           scroll_amount
       !           order_by_field_num
       !         Entity View imp_from child (Transient, Mandatory, Import only)
       !           cinstance_id
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !         Entity View imp_filter_start child (Transient, Mandatory, Import only)
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !         Entity View imp_filter_stop child (Transient, Mandatory, Import only)
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !         Entity View imp_filter child (Transient, Mandatory, Import only)
       !           csearch_attr_text
       !       EXPORTS:
       !         Group View (48) exp_group_list
       !           Entity View exp_g_list child (Transient, Export only)
       !             cinstance_id
       !             creference_id
       !             cparent_pkey_attr_text
       !             ckey_attr_num
       !             csearch_attr_text
       !             cother_attr_text
       !         Work View exp_error iyy1_component (Transient, Export only)
       !           severity_code
       !           rollback_indicator
       !           origin_servid
       !           context_string
       !           return_code
       !           reason_code
       !           checksum
       !       LOCALS:
       !         Work View loc_filter iyy1_list
       !           sort_option
       !           scroll_type
       !           list_direction
       !           scroll_amount
       !           order_by_field_num
       !         Entity View loc_from child
       !           ckey_attr_num
       !           cparent_pkey_attr_text
       !           cinstance_id
       !         Entity View loc_filter_start child
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !         Entity View loc_filter_stop child
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !         Entity View loc_filter child
       !           csearch_attr_text
       !         Group View (48) loc_group_list
       !           Entity View loc_g_list child
       !             cinstance_id
       !             creference_id
       !             cparent_pkey_attr_text
       !             ckey_attr_num
       !             csearch_attr_text
       !             cother_attr_text
       !         Entity View loc_empty child
       !           cinstance_id
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !         Entity View loc_max child
       !           cparent_pkey_attr_text
       !           ckey_attr_num
       !         Work View loc_read_lb iyy1_supplied
       !           total_integer
       !         Work View loc_error iyy1_component
       !           severity_code
       !           rollback_indicator
       !           origin_servid
       !           context_string
       !           return_code
       !           reason_code
       !           checksum
       !         Group View (9) loc_group_context
       !           Work View loc_g_context dont_change_text
       !             text_150
       !         Work View loc dont_change_return_codes
       !           1_ok
       !         Work View loc dont_change_reason_codes
       !           1_default
       !           11_list_full
       !           12_list_empty
       !         Work View loc_mvs_string_high_value iyy1_supplied
       !           flag
       !         Work View loc_pc_string_high_value iyy1_supplied
       !           flag
       !
       !     PROCEDURE STATEMENTS
       !
     1 !  NOTE:
     1 !  See the description for the purpose.
     1 !
     2 !  NOTE:
     2 !  RELEASE HISTORY
     2 !  01_00 23-02-1998 New release
     2 !
     3 !  USE cyyy9001_exception_hndlng_dflt
     3 !     WHICH EXPORTS: Work View exp_error iyy1_component FROM Work View exp_error iyy1_component
     4 !
     5 !  NOTE:
     5 !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
     5 !  Name the method below
     5 !
     6 !  SET SUBSCRIPT OF loc_group_context TO 1
     7 !  SET loc_g_context dont_change_text text_150 TO "CYYY0251"
     8 !  SET SUBSCRIPT OF loc_group_context TO 2
     9 !  SET loc_g_context dont_change_text text_150 TO "LIST"
    10 !
    11 !  NOTE:
    11 !  ****************************************************************
    11 !  The used ReturnCode/ReasonCode values
    11 !
    12 !  NOTE:
    12 !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    12 !  Set the return and reason codes below
    12 !
    13 !  SET loc dont_change_return_codes 1_ok TO 1
    14 !
    15 !  SET loc dont_change_reason_codes 1_default TO 1
    16 !
    17 !  NOTE:
    17 !  ****************************************************************
    17 !  a) The attribute(s) to be sorted must be in imp_from.
    17 !  b) If the attribute(s) to be sorted are not unique, the attribute(s) supplying them to be unique must be added.
    17 !  (Being defined as index in DB is not necessary)
    17 !  c) If there are multiple sorting options, common attribute(s) will naturally be in imp_form once.
    17 !  d) Attribute(s) to be sorted SORTED BY(s) must be included too, for uniqueness attribute(s) must be at the end if  added any.
    17 !  e) Attribute(s) in SORTED BY(s) must be first in WHERE condition in I1204such a manner that they oversee the same sequence.
    17 !
    18 !  +->IF exp_error iyy1_component return_code >= loc dont_change_return_codes 1_ok
    19 !  !  MOVE imp_filter iyy1_list TO loc_filter iyy1_list
    20 !  !
    21 !  !  +->IF loc_filter iyy1_list scroll_amount > MAX OF exp_group_list
    21 !  !  !        OR  loc_filter iyy1_list scroll_amount < 1
    22 !  !  !  SET loc_filter iyy1_list scroll_amount TO MAX OF exp_group_list
    21 !  !  +--
    23 !  !  +->IF loc_filter iyy1_list sort_option = SPACES
    24 !  !  !  SET loc_filter iyy1_list sort_option TO "ASC"
    23 !  !  +--
    25 !  !  +->IF loc_filter iyy1_list list_direction = "P"
    26 !  !  !  +->IF loc_filter iyy1_list sort_option = "ASC"
    27 !  !  !  !  SET loc_filter iyy1_list sort_option TO "DES"
    26 !  !  !  +> ELSE
    28 !  !  !  !  SET loc_filter iyy1_list sort_option TO "ASC"
    26 !  !  !  +--
    25 !  !  +--
    29 !  !
    30 !  !  NOTE:
    30 !  !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    30 !  !  Check over the filtering sections
    30 !  !
    31 !  !  NOTE:
    31 !  !  ****************************************************************
    31 !  !  Min-Max interval filtering
    31 !  !
    32 !  !  MOVE imp_filter_start child TO loc_filter_start child
    33 !  !  MOVE imp_filter_stop child TO loc_filter_stop child
    34 !  !
    35 !  !  NOTE:
    35 !  !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    35 !  !  Give the highest value that every key and filter_stop variable can get
    35 !  !
    36 !  !  SET loc_mvs_string_high_value iyy1_supplied flag TO "9"
    37 !  !  SET loc_pc_string_high_value iyy1_supplied flag TO "Z"
    38 !  !
    39 !  !  SET loc_max child ckey_attr_num TO 999999
    40 !  !  +->IF loc_pc_string_high_value iyy1_supplied flag > loc_mvs_string_high_value iyy1_supplied flag
    41 !  !  !  SET loc_max child cparent_pkey_attr_text TO "ZZZZZ"
    40 !  !  +> ELSE
    42 !  !  !  SET loc_max child cparent_pkey_attr_text TO "99999"
    40 !  !  +--
    43 !  !
    44 !  !  NOTE:
    44 !  !  ****************************************************************
    44 !  !  If the stop comes empty, it is accepted as maximum.
    44 !  !  If the stop comes occupied it cannot be less than Start, if it is less it is made equal to start.
    44 !  !
    45 !  !  +->IF loc_filter_stop child cparent_pkey_attr_text <= loc_empty child cparent_pkey_attr_text
    46 !  !  !  SET loc_filter_stop child cparent_pkey_attr_text TO loc_max child cparent_pkey_attr_text
    45 !  !  +> ELSE
    47 !  !  !  +->IF loc_filter_start child cparent_pkey_attr_text > loc_filter_stop child cparent_pkey_attr_text
    48 !  !  !  !  SET loc_filter_stop child cparent_pkey_attr_text TO loc_filter_start child cparent_pkey_attr_text
    47 !  !  !  +--
    45 !  !  +--
    49 !  !  +->IF loc_filter_stop child ckey_attr_num <= loc_empty child ckey_attr_num
    50 !  !  !  SET loc_filter_stop child ckey_attr_num TO loc_max child ckey_attr_num
    49 !  !  +> ELSE
    51 !  !  !  +->IF loc_filter_start child ckey_attr_num > loc_filter_stop child ckey_attr_num
    52 !  !  !  !  SET loc_filter_stop child ckey_attr_num TO loc_filter_start child ckey_attr_num
    51 !  !  !  +--
    49 !  !  +--
    53 !  !
    54 !  !  MOVE imp_from child TO loc_from child
    55 !  !
    56 !  !  NOTE:
    56 !  !  ****************************************************************
    56 !  !  From initial value is checked
    56 !  !
    57 !  !  +->IF loc_filter iyy1_list sort_option = "ASC"
    57 !  !  +> ELSE
    58 !  !  !  NOTE:
    58 !  !  !  ****************************************************************
    58 !  !  !  If From comes empty for Full list, it is made equal to Max.
    58 !  !  !
    59 !  !  !  +->IF loc_filter iyy1_list scroll_type ^= "S"
    60 !  !  !  !  NOTE:
    60 !  !  !  !  ****************************************************************
    60 !  !  !  !  If From comes empty for Full list, it is made equal to Max.
    60 !  !  !  !
    61 !  !  !  !  +->IF loc_from child cparent_pkey_attr_text <= loc_empty child cparent_pkey_attr_text
    61 !  !  !  !  !        AND  loc_from child ckey_attr_num <= loc_empty child ckey_attr_num
    62 !  !  !  !  !  SET loc_from child cparent_pkey_attr_text TO loc_max child cparent_pkey_attr_text
    63 !  !  !  !  !  SET loc_from child ckey_attr_num TO loc_max child ckey_attr_num
    61 !  !  !  !  +--
    59 !  !  !  +--
    57 !  !  +--
    64 !  !
    65 !  !  NOTE:
    65 !  !  ****************************************************************
    65 !  !  If the attributes in filter_start and filter_stop appear in from too, From cannot come greater than filter_Stop and

    65 !  !  less than filter_Start.
    65 !  !
    66 !  !  +->IF ((loc_from child cparent_pkey_attr_text < loc_filter_start child cparent_pkey_attr_text)
    66 !  !  !        OR  (loc_from child cparent_pkey_attr_text = loc_filter_start child cparent_pkey_attr_text
    66 !  !  !        AND  loc_from child ckey_attr_num < loc_filter_start child ckey_attr_num))
    66 !  !  !        OR  ((loc_from child cparent_pkey_attr_text > loc_filter_stop child cparent_pkey_attr_text)
    66 !  !  !        OR  (loc_from child cparent_pkey_attr_text = loc_filter_stop child cparent_pkey_attr_text
    66 !  !  !        AND  loc_from child ckey_attr_num > loc_filter_stop child ckey_attr_num))
    67 !  !  !
    68 !  !  !  +->IF loc_filter iyy1_list sort_option = "ASC"
    69 !  !  !  !  SET loc_from child cparent_pkey_attr_text TO loc_filter_start child cparent_pkey_attr_text
    70 !  !  !  !  SET loc_from child ckey_attr_num TO loc_filter_start child ckey_attr_num
    68 !  !  !  +> ELSE
    71 !  !  !  !  SET loc_from child cparent_pkey_attr_text TO loc_filter_stop child cparent_pkey_attr_text
    72 !  !  !  !  SET loc_from child ckey_attr_num TO loc_filter_stop child ckey_attr_num
    68 !  !  !  +--
    66 !  !  +--
    73 !  !
    74 !  !  NOTE:
    74 !  !  ****************************************************************
    74 !  !  Filtering (Compensate % as much as the filter variable length)
    74 !  !
    75 !  !  SET loc_filter child csearch_attr_text TO "%%%%%%%%%%%%%%%%%%%%%%%%%"
    76 !  !  SET loc_filter child csearch_attr_text TO concat(trim(imp_filter child csearch_attr_text),
    76 !  !              loc_filter child csearch_attr_text)
    77 !  !  NOTE:
    77 !  !  ****************************************************************
    77 !  !  END OF filtering
    77 !  !
    78 !  !  USE dyyy0251_child_list
    78 !  !     WHICH IMPORTS: Work View exp_error iyy1_component TO Work View imp_error iyy1_component
    78 !  !                    Work View loc_filter iyy1_list TO Work View imp_filter iyy1_list
    78 !  !                    Entity View loc_from child TO Entity View imp_from child
    78 !  !                    Entity View loc_filter_start child TO Entity View imp_filter_start child
    78 !  !                    Entity View loc_filter_stop child TO Entity View imp_filter_stop child
    78 !  !                    Entity View loc_filter child TO Entity View imp_filter child
    78 !  !     WHICH EXPORTS: Group View  loc_group_list FROM Group View exp_group_list
    78 !  !                    Work View loc_error iyy1_component FROM Work View exp_error iyy1_component
    79 !  !  +->IF loc_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    80 !  !  !  MOVE loc_error iyy1_component TO exp_error iyy1_component
    81 !  !  !  NOTE:
    81 !  !  !  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    81 !  !  !  !! WARNING: In D AB, if there' s a need for generating context string, must exit from ESCAPE AB.
    81 !  !  !
    82 ! <------ESCAPE
    79 !  !  +--
    83 !  !
    84 !  !  SET loc_read_lb iyy1_supplied total_integer TO LAST OF loc_group_list
    85 !  !  +->IF loc_read_lb iyy1_supplied total_integer > 0
    86 !  !  !  +->IF loc_filter iyy1_list list_direction = "P"
    87 !  !  !  !  SET SUBSCRIPT OF loc_group_list TO loc_read_lb iyy1_supplied total_integer
    86 !  !  !  +> ELSE
    88 !  !  !  !  SET SUBSCRIPT OF loc_group_list TO 1
    86 !  !  !  +--
    89 !  !  !  SET SUBSCRIPT OF exp_group_list TO 0
    90 !  !  !  +=>REPEAT
    91 !  !  !  !  SET SUBSCRIPT OF exp_group_list TO SUBSCRIPT OF exp_group_list + 1
    92 !  !  !  !  MOVE loc_g_list child TO exp_g_list child
    93 !  !  !  !  +->IF loc_filter iyy1_list list_direction = "P"
    94 !  !  !  !  !  SET SUBSCRIPT OF loc_group_list TO SUBSCRIPT OF loc_group_list - 1
    93 !  !  !  !  +> ELSE
    95 !  !  !  !  !  SET SUBSCRIPT OF loc_group_list TO SUBSCRIPT OF loc_group_list + 1
    93 !  !  !  !  +--
    96 !  !  !  !  SET loc_read_lb iyy1_supplied total_integer TO loc_read_lb iyy1_supplied total_integer - 1
    90 !  !  !  +--UNTIL loc_read_lb iyy1_supplied total_integer = 0
    85 !  !  +--
    18 !  +--
    97 !
    98 !  +->IF exp_error iyy1_component return_code < loc dont_change_return_codes 1_ok
    99 !  !  USE cyyy9141_context_string_set
    99 !  !     WHICH IMPORTS: Group View  loc_group_context TO Group View imp_group_context
    99 !  !     WHICH EXPORTS: Work View loc_error iyy1_component FROM Work View exp_error iyy1_component
    99 !  !                    Work View exp_error iyy1_component FROM Work View exp_context iyy1_component
   100 !  !
   101 !  !  +->IF loc_error iyy1_component return_code < loc dont_change_return_codes 1_ok
   102 !  !  !  MOVE loc_error iyy1_component TO exp_error iyy1_component
   101 !  !  +--
   103 !  !  SET exp_error iyy1_component severity_code TO "E"
    98 !  +--
       +---
